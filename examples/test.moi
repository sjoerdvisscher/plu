(
  "Quicksort:"

  # QSort sorts a sequence
  ?QSort = ?x,?xs =>
    xs Then (QSort(xs < x); x; QSort(xs >= x)) Else x

  QSort(5,2,3,6,4) Show 5
  
  # qSort sorts lists
  ?qSort = QSort.lift
  qSort({2,3}; {3,2}) show 5
)

(
  "Fibonacci:"

  ?zipWith = ?op ->
    ?x,?xs => ?y,?ys =>
      x + y
      xs zipWith(op) ys

  ?fib = 1, 1, fib zipWith(+) fib.Tail

  fib Show 8
)

(
  "Prime numbers:"

  ?primeFilter = ?p,?ns => p, primeFilter(ns[$._ mod p != 0])
  ?primeInput = 2, primeInput + 1

  primeFilter(primeInput) Show 7
)

(
  "Interleave:"
  
  ?Interleave = ?x,?xs => ?ys =>
    x Then (x; ys Interleave xs) Else ys
  
  ?evens = 0, 2 + evens
  ?odds = 1, 2 + odds
  
  (evens Interleave odds) Show 10
)

# Helper functions.

?Take = ?x,?xs => ?n ->
  (n != 0) Then (x; xs Take n - 1) Else ()

?Join = ?x,?xs => ?sep ->
  xs Then (x ++ sep ++ (xs Join sep)) Else x

?Show = ?xs => ?max -> xs Take max Join ", "
?show = ?xs -> ?max -> xs() Show max

?lift = ?f ->
  ?l -> { f(l()) }